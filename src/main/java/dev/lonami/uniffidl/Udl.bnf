{
  parserClass="dev.lonami.uniffidl.parser.UdlParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Udl"
  psiImplClassSuffix="Impl"
  psiPackage="dev.lonami.uniffidl.psi"
  psiImplPackage="dev.lonami.uniffidl.psi.impl"
  psiImplUtilClass="dev.lonami.uniffidl.psi.impl.UdlPsiImplUtil"

  elementTypeHolderClass="dev.lonami.uniffidl.psi.UdlTypes"
  elementTypeClass="dev.lonami.uniffidl.psi.UdlElementType"
  tokenTypeClass="dev.lonami.uniffidl.psi.UdlTokenType"

  tokens=[
      op_open_paren="("
      op_close_paren=")"
      op_wildcard="*"
      op_separator=","
      op_neg="-"
      kw_neginf="-Infinity"
      op_dot="."
      op_ellipsis="..."
      op_colon=":"
      op_semicolon=";"
      op_lessthan="<"
      op_equals="="
      op_greaterthan=">"
      op_nullable="?"
      kw_posinf="Infinity"
      kw_nan="NaN"
      op_open_bracket="["
      op_close_bracket="]"
      kw_any="any"
      kw_async="async"
      kw_attribute="attribute"
      kw_bigint="bigint"
      kw_boolean="boolean"
      kw_byte="byte"
      kw_callback="callback"
      kw_const="const"
      kw_constructor="constructor"
      kw_deleter="deleter"
      kw_dictionary="dictionary"
      kw_double="double"
      kw_enum="enum"
      kw_false="false"
      kw_float="float"
      kw_getter="getter"
      kw_includes="includes"
      kw_inherit="inherit"
      kw_interface="interface"
      kw_iterable="iterable"
      kw_long="long"
      kw_maplike="maplike"
      kw_mixin="mixin"
      kw_namespace="namespace"
      kw_null="null"
      kw_object="object"
      kw_octet="octet"
      kw_optional="optional"
      kw_or="or"
      kw_partial="partial"
      kw_readonly="readonly"
      kw_record="record"
      kw_required="required"
      kw_sequence="sequence"
      kw_setlike="setlike"
      kw_setter="setter"
      kw_short="short"
      kw_static="static"
      kw_string="string"
      kw_stringifier="stringifier"
      kw_symbol="symbol"
      kw_true="true"
      kw_typedef="typedef"
      kw_undefined="undefined"
      kw_unrestricted="unrestricted"
      kw_unsigned="unsigned"
      op_open_brace="{"
      op_pipe="|"
      op_close_brace="}"

      integer='regexp:-?([1-9][0-9]*|0[Xx][0-9A-Fa-f]+|0[0-7]*)'
      decimal='regexp:-?(([0-9]+\.[0-9]*|[0-9]*\.[0-9]+)([Ee][+-]?[0-9]+)?|[0-9]+[Ee][+-]?[0-9]+)'
      identifier='regexp:[_-]?[A-Za-z][0-9A-Z_a-z-]*'
      string='regexp:\"[^\"]*\"'
      whitespace='regexp:[\t\n\r ]+'
      comment='regexp://.*|/\*(.|\n)*?\*/'
      alt='regexp:[^\t\n\r 0-9A-Za-z]'
  ]
}

// Most of UDL comes from WebIDL's grammar at https://webidl.spec.whatwg.org/#idl-grammar

Definitions ::=
    (ExtendedAttributeList Definition Definitions)?

Definition ::=
    CallbackOrInterfaceOrMixin
    | Namespace
    | Partial
    | Dictionary
    | Enum
    | Typedef
    | IncludesStatement

ArgumentNameKeyword ::=
    kw_async
    | kw_attribute
    | kw_callback
    | kw_const
    | kw_constructor
    | kw_deleter
    | kw_dictionary
    | kw_enum
    | kw_getter
    | kw_includes
    | kw_inherit
    | kw_interface
    | kw_iterable
    | kw_maplike
    | kw_mixin
    | kw_namespace
    | kw_partial
    | kw_readonly
    | kw_required
    | kw_setlike
    | kw_setter
    | kw_static
    | kw_stringifier
    | kw_typedef
    | kw_unrestricted

CallbackOrInterfaceOrMixin ::=
    kw_callback CallbackRestOrInterface
    | kw_interface InterfaceOrMixin

InterfaceOrMixin ::=
    InterfaceRest
    | MixinRest

InterfaceRest ::=
    identifier Inheritance op_open_brace InterfaceMembers op_close_brace op_semicolon

Partial ::=
    kw_partial PartialDefinition

PartialDefinition ::=
    kw_interface PartialInterfaceOrPartialMixin
    | PartialDictionary
    | Namespace

PartialInterfaceOrPartialMixin ::=
    PartialInterfaceRest
    | MixinRest

PartialInterfaceRest ::=
    identifier op_open_brace PartialInterfaceMembers op_close_brace op_semicolon

InterfaceMembers ::=
    (ExtendedAttributeList InterfaceMember InterfaceMembers)?

InterfaceMember ::=
    PartialInterfaceMember
    | Constructor

PartialInterfaceMembers ::=
    (ExtendedAttributeList PartialInterfaceMember PartialInterfaceMembers)?

PartialInterfaceMember ::=
    Const
    | Operation
    | Stringifier
    | StaticMember
    | Iterable
    | AsyncIterable
    | ReadOnlyMember
    | ReadWriteAttribute
    | ReadWriteMaplike
    | ReadWriteSetlike
    | InheritAttribute

Inheritance ::=
    (op_colon identifier)?

MixinRest ::=
    kw_mixin identifier op_open_brace MixinMembers op_close_brace op_semicolon

MixinMembers ::=
    (ExtendedAttributeList MixinMember MixinMembers)?

MixinMember ::=
    Const
    | RegularOperation
    | Stringifier
    | OptionalReadOnly AttributeRest

IncludesStatement ::=
    identifier kw_includes identifier op_semicolon

CallbackRestOrInterface ::=
    CallbackRest
    | kw_interface identifier op_open_brace CallbackInterfaceMembers op_close_brace op_semicolon

CallbackInterfaceMembers ::=
    (ExtendedAttributeList CallbackInterfaceMember CallbackInterfaceMembers)?

CallbackInterfaceMember ::=
    Const
    | RegularOperation

Const ::=
    kw_const ConstType identifier op_equals ConstValue op_semicolon

ConstValue ::=
    BooleanLiteral
    | FloatLiteral
    | integer

BooleanLiteral ::=
    kw_true
    | kw_false

FloatLiteral ::=
    decimal
    | kw_neginf
    | kw_posinf
    | kw_nan

ConstType ::=
    PrimitiveType
    | identifier

ReadOnlyMember ::=
    kw_readonly ReadOnlyMemberRest

ReadOnlyMemberRest ::=
    AttributeRest
    | MaplikeRest
    | SetlikeRest

ReadWriteAttribute ::=
    AttributeRest

InheritAttribute ::=
    kw_inherit AttributeRest

AttributeRest ::=
    kw_attribute TypeWithExtendedAttributes AttributeName op_semicolon

AttributeName ::=
    AttributeNameKeyword
    | identifier

AttributeNameKeyword ::=
    kw_async
    | kw_required

OptionalReadOnly ::=
    (kw_readonly)?

DefaultValue ::=
    ConstValue
    | kw_string
    | op_open_bracket op_close_bracket
    | op_open_brace op_close_brace
    | kw_null
    | kw_undefined

Operation ::=
    RegularOperation
    | SpecialOperation

RegularOperation ::=
    Type OperationRest

SpecialOperation ::=
    Special RegularOperation

Special ::=
    kw_getter
    | kw_setter
    | kw_deleter

OperationRest ::=
    OptionalOperationName op_open_paren ArgumentList op_close_paren op_semicolon

OptionalOperationName ::=
    (OperationName)?

OperationName ::=
    OperationNameKeyword
    | identifier

OperationNameKeyword ::=
    kw_includes

ArgumentList ::=
    (Argument Arguments)?

Arguments ::=
    (op_separator Argument Arguments)?

Argument ::=
    ExtendedAttributeList ArgumentRest

ArgumentRest ::=
    kw_optional TypeWithExtendedAttributes ArgumentName Default
    | Type Ellipsis ArgumentName

ArgumentName ::=
    ArgumentNameKeyword
    | identifier

Ellipsis ::=
    (op_ellipsis)?

Constructor ::=
    kw_constructor op_open_paren ArgumentList op_close_paren op_semicolon

Stringifier ::=
    kw_stringifier StringifierRest

StringifierRest ::=
    OptionalReadOnly AttributeRest
    | op_semicolon

StaticMember ::=
    kw_static StaticMemberRest

StaticMemberRest ::=
    OptionalReadOnly AttributeRest
    | RegularOperation

Iterable ::=
    kw_iterable op_lessthan TypeWithExtendedAttributes OptionalType op_greaterthan op_semicolon

OptionalType ::=
    (op_separator TypeWithExtendedAttributes)?

AsyncIterable ::=
    kw_async kw_iterable op_lessthan TypeWithExtendedAttributes OptionalType op_greaterthan OptionalArgumentList op_semicolon

OptionalArgumentList ::=
    (op_open_paren ArgumentList op_close_paren)?

ReadWriteMaplike ::=
    MaplikeRest

MaplikeRest ::=
    kw_maplike op_lessthan TypeWithExtendedAttributes op_separator TypeWithExtendedAttributes op_greaterthan op_semicolon

ReadWriteSetlike ::=
    SetlikeRest

SetlikeRest ::=
    kw_setlike op_lessthan TypeWithExtendedAttributes op_greaterthan op_semicolon

Namespace ::=
    kw_namespace identifier op_open_brace NamespaceMembers op_close_brace op_semicolon

NamespaceMembers ::=
    (ExtendedAttributeList NamespaceMember NamespaceMembers)?

NamespaceMember ::=
    RegularOperation
    | kw_readonly AttributeRest
    | Const

Dictionary ::=
    kw_dictionary identifier Inheritance op_open_brace DictionaryMembers op_close_brace op_semicolon {
        mixin="dev.lonami.uniffidl.psi.impl.UdlNamedElementImpl"
        implements="dev.lonami.uniffidl.psi.UdlNamedElement"
        methods=[getNameIdentifier getName setName getPresentation]
    }

DictionaryMembers ::=
    (DictionaryMember DictionaryMembers)?

DictionaryMember ::=
    ExtendedAttributeList DictionaryMemberRest

DictionaryMemberRest ::=
    kw_required TypeWithExtendedAttributes identifier op_semicolon
    | Type identifier Default op_semicolon

PartialDictionary ::=
    kw_dictionary identifier op_open_brace DictionaryMembers op_close_brace op_semicolon

Default ::=
    (op_equals DefaultValue)?

Enum ::=
    kw_enum identifier op_open_brace EnumValueList op_close_brace op_semicolon

EnumValueList ::=
    kw_string EnumValueListComma

EnumValueListComma ::=
    (op_separator EnumValueListString)?

EnumValueListString ::=
    (kw_string EnumValueListComma)?

CallbackRest ::=
    identifier op_equals Type op_open_paren ArgumentList op_close_paren op_semicolon

Typedef ::=
    kw_typedef TypeWithExtendedAttributes identifier op_semicolon

Type ::=
    SingleType
    | UnionType Null

TypeWithExtendedAttributes ::=
    ExtendedAttributeList Type

SingleType ::=
    DistinguishableType
    | kw_any
    | PromiseType

UnionType ::=
    op_open_paren UnionMemberType kw_or UnionMemberType UnionMemberTypes op_close_paren

UnionMemberType ::=
    ExtendedAttributeList DistinguishableType
    | UnionType Null

UnionMemberTypes ::=
    (kw_or UnionMemberType UnionMemberTypes)?

DistinguishableType ::=
    PrimitiveType Null
    | StringType Null
    | identifier Null
    | kw_sequence op_lessthan TypeWithExtendedAttributes op_greaterthan Null
    | kw_object Null
    | kw_symbol Null
    | BufferRelatedType Null
    | "FrozenArray" op_lessthan TypeWithExtendedAttributes op_greaterthan Null
    | "ObservableArray" op_lessthan TypeWithExtendedAttributes op_greaterthan Null
    | RecordType Null
    | kw_undefined Null

PrimitiveType ::=
    UnsignedIntegerType
    | UnrestrictedFloatType
    | kw_boolean
    | kw_byte
    | kw_octet
    | kw_bigint

UnrestrictedFloatType ::=
    kw_unrestricted FloatType
    | FloatType

FloatType ::=
    kw_float
    | kw_double

UnsignedIntegerType ::=
    kw_unsigned IntegerType
    | IntegerType

IntegerType ::=
    kw_short
    | kw_long OptionalLong

OptionalLong ::=
    (kw_long)?

StringType ::=
    "ByteString"
    | "DOMString"
    | "USVString"

PromiseType ::=
    "Promise" op_lessthan Type op_greaterthan

RecordType ::=
    kw_record op_lessthan StringType op_separator TypeWithExtendedAttributes op_greaterthan

Null ::=
    (op_nullable)?

BufferRelatedType ::=
    "ArrayBuffer"
    | "DataView"
    | "Int8Array"
    | "Int16Array"
    | "Int32Array"
    | "Uint8Array"
    | "Uint16Array"
    | "Uint32Array"
    | "Uint8ClampedArray"
    | "BigInt64Array"
    | "BigUint64Array"
    | "Float32Array"
    | "Float64Array"

ExtendedAttributeList ::=
    (op_open_bracket ExtendedAttribute ExtendedAttributes op_close_bracket)?

ExtendedAttributes ::=
    (op_separator ExtendedAttribute ExtendedAttributes)?

ExtendedAttribute ::=
    op_open_paren ExtendedAttributeInner op_close_paren ExtendedAttributeRest
    | op_open_bracket ExtendedAttributeInner op_close_bracket ExtendedAttributeRest
    | op_open_brace ExtendedAttributeInner op_close_brace ExtendedAttributeRest
    | Other ExtendedAttributeRest

ExtendedAttributeRest ::=
    (ExtendedAttribute)?

ExtendedAttributeInner ::=
    (op_open_paren ExtendedAttributeInner op_close_paren ExtendedAttributeInner
    | op_open_bracket ExtendedAttributeInner op_close_bracket ExtendedAttributeInner
    | op_pipe op_open_brace ExtendedAttributeInner op_close_brace ExtendedAttributeInner
    | op_pipe OtherOrComma ExtendedAttributeInner)?

Other ::=
    integer
    | decimal
    | identifier
    | string
    | alt
    | op_neg
    | kw_neginf
    | op_dot
    | op_ellipsis
    | op_colon
    | op_semicolon
    | op_lessthan
    | op_equals
    | op_greaterthan
    | op_nullable
    | op_wildcard
    | "ByteString"
    | "DOMString"
    | "FrozenArray"
    | kw_posinf
    | kw_nan
    | "ObservableArray"
    | "Promise"
    | "USVString"
    | kw_any
    | kw_bigint
    | kw_boolean
    | kw_byte
    | kw_double
    | kw_false
    | kw_float
    | kw_long
    | kw_null
    | kw_object
    | kw_octet
    | kw_or
    | kw_optional
    | kw_record
    | kw_sequence
    | kw_short
    | kw_symbol
    | kw_true
    | kw_unsigned
    | kw_undefined
    | ArgumentNameKeyword
    | BufferRelatedType

OtherOrComma ::=
    Other
    | op_separator

IdentifierList ::=
    identifier Identifiers

Identifiers ::=
    (op_separator identifier Identifiers)?

ExtendedAttributeNoArgs ::=
    identifier

ExtendedAttributeArgList ::=
    identifier op_open_paren ArgumentList op_close_paren

ExtendedAttributeIdent ::=
    identifier op_equals identifier

ExtendedAttributeWildcard ::=
    identifier op_equals op_wildcard

ExtendedAttributeIdentList ::=
    identifier op_equals op_open_paren IdentifierList op_close_paren

ExtendedAttributeNamedArgList ::=
    identifier op_equals identifier op_open_paren ArgumentList op_close_paren
